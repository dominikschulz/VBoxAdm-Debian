package VBoxAdm::DB;

use strict;
use warnings;

use Carp;
use DBI qw(:sql_types);

our $VERSION = '@VERSION@';

############################################
# Usage      : VBoxAdm::DB->show_child_handles($dbh);
# Purpose    : Recursively prints all child handles of the given handle.
# Returns    : nothing
# Parameters : One DBI handle
# Throws     : no exceptions
# Comments   : none
# See Also   : http://search.cpan.org/~timb/DBI-1.611/DBI.pm#ChildHandles_%28array_ref%29
sub show_child_handles {
    my $h = shift;
    my $level = shift || 0;
    printf "%sh %s %s\n", $h->{Type}, "\t" x $level, $h;
    &show_child_handles( $_, $level + 1 ) for ( grep { defined } @{ $h->{ChildHandles} } );
    return;
}

############################################
# Usage      : VBoxAdm::DB->finish_child_handles($dbh);
# Purpose    : Recursivly (up to 10 levels of nesting) visit all child handles an call finish() on them.
# Returns    : nothing
# Parameters : One DBI handle
# Throws     : no exceptions
# Comments   : none
# See Also   : http://search.cpan.org/~timb/DBI-1.611/DBI.pm#ChildHandles_%28array_ref%29
sub finish_child_handles {
    my $handle = shift;
    my $level = shift || 0;

    # prevent infinite loops
    return if $level > 10;
    foreach my $child ( grep { defined } @{ $handle->{ChildHandles} } ) {
        if ( $child->{Type} eq "st" ) {    # st -> (prepared) Statement
            $child->finish();
        }
        else {
            &finish_child_handles( $child, $level + 1 );
        }
    }
    return;
}

############################################
# Usage      : my $dbh = VBoxAdm::DB->connect($dsn);
# Purpose    : Connect to a Database
# Returns    : a valid DBI database handle
# Parameters : $dsn - the dsn
# Throws     : Exception if connection fails
# Comments   : none
# See Also   : n/a
sub connect {
    my $dsn  = shift;
    my $opts = shift || {};
    my $dbh  = undef;

    my $timeout = $opts->{ConnectTimeout} || 30;
    eval {
        local $SIG{ALRM} = sub { die "alarm\n" };
        my $prev_timeout = alarm $timeout;
        $dbh = DBI->connect_cached( $dsn, undef, undef, { RaiseError => 0, PrintError => 0, } );
        alarm $prev_timeout;
    };

    # No eval error handling. If the connection timed out $dbh will be undef anyway ...
    if ($dbh) {
        $dbh->{HandleError} = sub { confess(shift) };
        if ( !$opts->{NoUTF8} ) {
            $dbh->do('SET NAMES utf8');
        }
        return $dbh;
    }
    else {
        my $msg = "Connection to DB failed with DSN $dsn: " . DBI->errstr;
        warn $msg;
        return;
    }
}

############################################
# Usage      : my $sth = VBoxAdm::prepexec($dbh, $query, @params);
# Purpose    : Prepare and execute a statement
# Returns    : ????
# Parameters : ????
# Throws     : Exception on error
# Comments   : none
# See Also   : n/a
sub prepexec {
    my ( $dbh, $sqlstr, @params ) = @_;
    my $sth;
    if ( $sth = $dbh->prepare($sqlstr) ) {
        if ( $sth->execute(@params) ) {
            return $sth;
        }
    }
    return;
}

1;
__END__

=head1 NAME

VBoxAdm::DB - common DB methods for VBoxAdm

=head1 VERSION

This documentation refers to VBoxAdm::DB version @VERSION@
built on @BUILDDATE@.

=head1 SYNOPSIS

    use VBoxAdm::DB;

=head1 DESCRIPTION

This class provides common DB methods.

=head1 DEPENDENCIES

DBI.

=head1 INCOMPATIBILITIES

None known.

=head1 BUGS AND LIMITATIONS

There are no known bugs in this module.
Please report problems to Dominik Schulz (dominik.schulz@gauner.org)
Patches are welcome.

=head1 AUTHOR

Dominik Schulz (dominik.schulz@gauner.org)

=head1 LICENCE AND COPYRIGHT

Copyright (c) 2010 Dominik Schulz (dominik.schulz@gauner.org). All rights reserved.

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 2 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.

=cut
