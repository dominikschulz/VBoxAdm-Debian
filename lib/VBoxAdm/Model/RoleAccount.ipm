package VBoxAdm::Model::RoleAccount;

use Moose;
use namespace::autoclean;

use Carp;

use Sys::Syslog;
use VBoxAdm::DB '@VERSION@';
use VBoxAdm::Utils '@VERSION@';

extends 'VBoxAdm::Model';

sub _init_fields { return [qw(id name local_part domain ts)]; }

sub create {
    my ( $self, $role, $goto ) = @_;

    # Authorization - No access for regular users
    if ( !$self->user->is_siteadmin() ) {
        $self->msg->push( 'error', 'Sorry. No access for regular users.' );
        return;
    }

    my $delim = $self->config()->{'smtpproxy'}->{'recipient_delimiter'};

    if ( !&VBoxAdm::Utils::is_valid_localpart_rfc822($role) ) {
        $self->msg->push( 'error', 'Invalid localpart as role name given.' );
        return;
    }
    elsif ( !&VBoxAdm::Utils::is_valid_address_rfc822($goto) ) {
        $self->msg->push( 'error', 'Invalid email address given.' );
        return;
    }
    elsif ( $role && $delim && $role =~ m/\Q$delim\E/ ) {
        $self->msg->push( 'error', 'Localpart may not contain the recipent_delimiter (' . $delim . ')' );
        return;
    }
    my ( $local_part, $domain ) = split /@/, $goto;

    my $sql = "INSERT IGNORE INTO role_accounts (name,local_part,domain,ts) VALUES(?,?,?,NOW())";
    my $sth = &VBoxAdm::DB::prepexec( $self->dbh, $sql, $role, $local_part, $domain );

    if ( !$sth ) {
        $self->msg->push( 'error', 'Database error.' );
        return;
    }

    $sth->finish();

    return 1;
}

sub get_id {
    my ( $self, $role ) = @_;

    # Authorization - No access for regular users
    if ( !$self->user->is_siteadmin() ) {
        $self->msg->push( 'error', 'Sorry. No access for regular users.' );
        return;
    }

    my $sql = "SELECT id FROM role_accounts WHERE name = ?";
    my $sth = &VBoxAdm::DB::prepexec( $self->dbh, $sql, $role );
    my $id  = $sth->fetchrow_array();

    if ( !$sth ) {
        $self->msg->push( 'error', 'Database error.' );
        return;
    }

    $sth->finish();

    return $id;
}

sub update {
    my ( $self, $entry_id, $params ) = @_;

    # Authorization - No access for regular users
    if ( !$self->user->is_siteadmin() ) {
        $self->msg->push( 'error', 'Sorry. No access for regular users.' );
        return;
    }
    my $goto = $params->{'Goto'};
    if ( !&VBoxAdm::Utils::is_valid_address_rfc822($goto) ) {
        $self->msg->push( 'error', 'Invalid email address given.' );
        return;
    }
    my ( $local_part, $domain ) = split /@/, $goto;

    if ( $entry_id && defined($goto) ) {
        my $query = "UPDATE role_accounts SET local_part = ?, domain = ? WHERE id = ?";
        if ( my $sth = $self->dbh->prepare($query) ) {
            if ( $sth->execute( $local_part, $domain, $entry_id ) ) {
                $sth->finish();
                $self->msg->push( 'information', "Updated entry [_1]. Set target to[_2]@[_3].", $entry_id, $local_part, $domain );
                return 1;
            }
            else {
                $self->msg->push( 'error', "Unable to update RoleAccount. Database Error #0E." );
                return;
            }
        }
        else {
            $self->msg->push( 'error', "Unable to update RoleAccount. Database Error #0P" );
            return;
        }
    }

    return;
}

sub delete {
    my ( $self, $entry_id ) = @_;

    # Authorization - No access for regular users
    if ( !$self->user->is_siteadmin() ) {
        $self->msg->push( 'error', 'Sorry. No access for regular users.' );
        return;
    }

    # delete role account
    if ( $entry_id && $entry_id =~ m/^\d+$/ ) {
        my $query = "DELETE FROM role_accounts WHERE id = ?";
        if ( my $sth = $self->dbh->prepare($query) ) {
            if ( $sth->execute($entry_id) ) {
                $sth->finish();
                $self->msg->push( 'information', "Deleted entry [_1].", $entry_id );
                return 1;
            }
        }
        else {
            $self->msg->push( 'error', "Unable to delete RoleAccount $entry_id. Database Error." );
        }
    }
}

sub read {
    my ( $self, $id ) = @_;

    # Authorization - No access for regular users
    if ( !$self->user->is_siteadmin() ) {
        $self->msg->push( 'error', 'Sorry. No access for regular users.' );
        return;
    }

    return $self->_read( 'role_accounts', $id, );
}

sub list {
    my ( $self, $params ) = @_;

    # Authorization - No access for regular users
    if ( !$self->user->is_siteadmin() ) {
        $self->msg->push( 'error', 'Sorry. No access for regular users.' );
        return;
    }

    return $self->_list( 'role_accounts', $params );
}

no Moose;
__PACKAGE__->meta->make_immutable;

1;
__END__

=head1 NAME

VBoxAdm::Model::RoleAccount - Class for RoleAccount

=head1 VERSION

This documentation refers to VBoxAdm::Model::RoleAccount version @VERSION@
built on @BUILDDATE@.

=head1 SYNOPSIS

    use VBoxAdm::Model::RoleAccount;

=head1 DESCRIPTION

TODO

=head1 DEPENDENCIES

DBI.

=head1 INCOMPATIBILITIES

None known.

=head1 BUGS AND LIMITATIONS

There are no known bugs in this module.
Please report problems to Dominik Schulz (dominik.schulz@gauner.org)
Patches are welcome.

=head1 AUTHOR

Dominik Schulz (dominik.schulz@gauner.org)

=head1 LICENCE AND COPYRIGHT

Copyright (c) 2010 Dominik Schulz (dominik.schulz@gauner.org). All rights reserved.

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 2 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.

=cut
