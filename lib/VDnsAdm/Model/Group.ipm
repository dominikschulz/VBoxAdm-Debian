package VDnsAdm::Model::Group;

use Moose;
use namespace::autoclean;

use Carp;

use VWebAdm::DB '@VERSION@';
use VWebAdm::Utils '@VERSION@';

extends 'VWebAdm::Model';

has 'table_name' => (
    'is'      => 'ro',
    'isa'     => 'Str',
    'lazy'    => 1,
    'builder' => '_init_table_name',
);

sub _init_table_name {
    my $self = shift;

    return 'groups';
}

sub _init_fields {
    return [qw(id name)];
}

sub _validate_params {
    my $self   = shift;
    my $params = shift;

    #
    # Name
    #
    if ( defined( $params->{'name'} ) ) {
        if ( $params->{'name'} !~ m/^[\w.]{1,255}$/i ) {
            $self->msg->push( 'error', 'Group name out of range!' );
            return;
        }
    }

    return 1;
}

sub create {
    my ( $self, $params ) = @_;

    # Authorization - No access for regular users
    if ( !$self->user->is_siteadmin() ) {
        $self->msg->push( 'error', 'Sorry. No access for regular users.' );
        return;
    }

    if ( !$self->_validate_params($params) ) {
        return;
    }

    my $query = "INSERT INTO " . $self->table_name() . " (" . join( ',', keys %{$params} ) . ") VALUES(";
    $query .= "?," x ( scalar( keys %{$params} ) - 1 );
    $query .= "?)";

    if ( my $sth = &VWebAdm::DB::prepexec( $self->dbh, $query, values %{$params} ) ) {
        my $domain_id = $self->dbh->last_insert_id( undef, undef, undef, undef );
        $self->msg->push( 'information', "Group '[_1]' added", $params->{'name'} );
        $sth->finish();
        return $domain_id;
    }
    else {
        $self->logger()->log( 'Could not execute Query: ' . $query . ', Args: ' . $params->{'name'} . ', Error: ' . $self->dbh()->errstr() );
        $self->msg->push( 'error', "Failed to add Group '[_1]'. Database error.", $params->{'name'} );
        return;
    }
}

sub delete {
    my ( $self, $domain_id ) = @_;

    # Authorization - No access for regular users
    if ( !$self->user->is_siteadmin() ) {
        $self->msg->push( 'error', 'Sorry. No access for regular users.' );
        return;
    }

    my $sql = "DELETE FROM " . $self->table_name() . " WHERE id = ? LIMIT 1";
    my $sth = &VWebAdm::DB::prepexec( $self->dbh, $sql, $domain_id );

    if ( !$sth ) {
        $self->logger()->log( message => 'Could not execute query ' . $sql . ' due to error: ' . $self->dbh()->errstr, level => 'error', );
        $self->msg->push( 'error', 'Database error.' );
        return;
    }

    $sth->finish();

    return 1;
}

sub update {
    my ( $self, $group_id, $params ) = @_;

    # Authorization - No access for regular users
    if ( !$self->user->is_siteadmin() ) {
        $self->msg->push( 'error', 'Sorry. No access for regular users.' );
        return;
    }

    my $group_name = $self->get_name($group_id);

    if ( !$self->_validate_params($params) ) {
        $self->log('Params not valid!');
        return;
    }

    my $query = "UPDATE " . $self->table_name() . " SET ";

    # only execute query if scalar(@params) > 0, i.e. at least on valid option
    my @args = ();

    foreach my $key ( keys %{$params} ) {
        $params->{$key} = Encode::decode( 'UTF-8', $params->{$key} );
        $query .= $key . ' = ?, ';
        push( @args, $params->{$key} );
    }

    # at least on option has changed
    if ( scalar(@args) > 0 ) {

        # remove (possible) trailing ,
        $query =~ s/,\s*$//;

        # finialize query
        $query .= " WHERE id = ?";
        push( @args, $group_id );

        if ( my $sth = &VWebAdm::DB::prepexec( $self->dbh, $query, @args ) ) {
            $sth->finish();
            $self->msg->push( 'information', 'Updated Group [_1].', $group_name );
            return 1;
        }
        else {
            $self->log( 'Could not execute Query: ' . $query . ', Args: ' . join( ',', @args ) . ', Error: ' . $self->dbh->errstr );
            $self->msg->push( 'error', 'Failed to update Group [_1]. Database error.', $group_name );
            return;
        }
    }
}

sub read {
    my ( $self, $id ) = @_;

    # Authorization - No access for regular users
    if ( !$self->user->is_admin() ) {
        $self->msg->push( 'error', 'Sorry. No access for regular users.' );
        return;
    }
    elsif ( !$self->user->is_siteadmin() && $self->user->get_domain_id() != $id ) {
        $self->msg->push( 'error', 'Sorry. Not your domain.' );
        return;
    }

    return $self->_read( $self->table_name(), $id, );
}

sub list {
    my ( $self, $params ) = @_;

    # Authorization - No access for regular users
    if ( !$self->user->is_admin() ) {
        $self->msg->push( 'error', 'Sorry. No access for regular users.' );
        return;
    }
    elsif ( !$self->user->is_siteadmin() ) {

        # limit to this domain
        #$params->{'id'} = $self->user->get_domain_id();
        # TODO restrict a user to his groups ...
    }

    return $self->_list( $self->table_name(), $params );
}

sub get_name {
    my ( $self, $id ) = @_;

    # Authorization - No access for regular users
    if ( !$self->user->is_admin() ) {
        $self->msg->push( 'error', 'Sorry. No access for regular users.' );
        return;
    }

    my $query = "SELECT name FROM " . $self->table_name() . " WHERE id = ?";
    my $sth = &VWebAdm::DB::prepexec( $self->dbh, $query, $id );

    if ( !$sth ) {
        $self->logger()->log( message => 'Could not execute query ' . $query . ' due to error: ' . $self->dbh()->errstr, level => 'error', );
        $self->msg->push( 'error', 'Database error.' );
        return;
    }

    my $domain_name = $sth->fetchrow_array();
    $sth->finish();

    return $domain_name;
}

sub get_id {
    my ( $self, $name ) = @_;

    # Authorization - No access for regular users
    if ( !$self->user->is_admin() ) {
        $self->msg->push( 'error', 'Sorry. No access for regular users.' );
        return;
    }

    my $sql = "SELECT id FROM " . $self->table_name() . " WHERE name = ?";
    my $sth = &VWebAdm::DB::prepexec( $self->dbh, $sql, $name );

    if ( !$sth ) {
        $self->logger()->log( message => 'Could not execute query ' . $sql . ' due to error: ' . $self->dbh()->errstr, level => 'error', );
        $self->msg->push( 'error', 'Database error.' );
        return;
    }

    my $domain_id = $sth->fetchrow_array();
    $sth->finish();

    return $domain_id;
}

no Moose;
__PACKAGE__->meta->make_immutable;

1;
